package CO::NGSPipeline::BSseq::BSMAP;

use strict;
use CO::NGSPipeline::BSseq::Config;
use CO::NGSPipeline::Utils;

use base qw(CO::NGSPipeline::Common);

sub new {
	my $class = shift;
	$class = ref($class) ? ref($class) : $class;
	
	my $pipeline = shift;
	
	my $self = {"pipeline" => $pipeline};
	
	return bless $self, $class;
}

=item C<$self-E<gt>align(HASH)>

BSMAP alignment

  fastq1        path of read 1
  fastq2        path of read 2
  output        path of output
  delete_input  whether delete input files
 
=cut
sub align {
	my $self = shift;
	
	my %param = ( "fastq1" => undef,
	              "fastq2" => undef,
				  "output" => undef,
				  "delete_input" => 0,
				  @_);
	
	my $fastq1 = to_abs_path($param{fastq1});
	my $fastq2 = to_abs_path($param{fastq2});
	my $output = to_abs_path($param{output});
	my $delete_input = $param{delete_input};
	
	unless($output =~/\.bam$/) {
		die "Only permit outputting bam file in BSMAP alignment.\n";
	}
	
	my $pipeline = $self->{pipeline};
	
	$pipeline->add_command("PATH=\$PATH:$BSMAP_BIN_DIR/", 0);
	my $random_str = time().int(rand(9999999));
	$pipeline->add_command("mkfifo \$PBS_SCRATCH_DIR/\$PBS_JOBID/$random_str.sam", 0);
	$pipeline->add_command("bsmap -a $fastq1 -b $fastq2 -d $BSMAP_GENOME_DIR/$BSMAP_REF_GENOME -o \$PBS_SCRATCH_DIR/\$PBS_JOBID/$random_str.sam -p 8 -r 0 -v 8 &"); # unique match, at most 8 mismatches
	
	my $output_base = $output; $output_base =~s/\.bam$//;
	$pipeline->add_command("samtools view -uSbh \$PBS_SCRATCH_DIR/\$PBS_JOBID/$random_str.sam | mbuffer -q -m 2G -l /dev/null | samtools sort - $output_base");
	$pipeline->add_command("samtools index $output");
	$pipeline->add_command("rm \$PBS_SCRATCH_DIR/\$PBS_JOBID/$random_str.sam", 0);
	
	$pipeline->del_file($fastq1, $fastq2) if($delete_input);
	$pipeline->check_filesize($output); # 1M
	my $qid = $pipeline->run("-N" => $pipeline->get_job_name ? $pipeline->get_job_name : "_bsmap_align",
							 "-l" => { nodes => "1:ppn=8:lsdf", 
									    mem => "12GB",
										walltime => "150:00:00"});

	return($qid);
}

sub lambda_conversion {
	my $self = shift;
	
	my %param = ( "bam" => undef,
	               "output" => undef,
	               "delete_input" => 0,
				   @_);
	
	my $bam    = to_abs_path($param{bam});
	my $output = to_abs_path($param{output});
	my $delete_input = $param{delete_input};
	
	my $pipeline = $self->{pipeline};
	
	$pipeline->add_command("samtools view -h $bam lambda > $pipeline->{tmp_dir}/lambda.sam");
	$pipeline->add_command("python $BSMAP_BIN_DIR/methratio.py -d $BSMAP_GENOME_DIR/$BSMAP_REF_GENOME --chr=lambda -o $pipeline->{tmp_dir}/lambda.out -p $pipeline->{tmp_dir}/lambda.sam -z");
	$pipeline->add_command("cat $pipeline->{tmp_dir}/lambda.out | awk '{a+=\$5}END{print 1-a/(NR-1)}' > $output", 0);
	
	$pipeline->del_file("$pipeline->{tmp_dir}/lambda.sam");
	$pipeline->del_file("$pipeline->{tmp_dir}/lambda.out");

	my $qid = $pipeline->run("-N" => $pipeline->get_job_name ? $pipeline->get_job_name : "_bsmap_lambda_conversion",
							 "-l" => { nodes => "1:ppn=1:lsdf", 
									    mem => "1GB",
										walltime => "2:00:00"});

	return($qid);

}

=item C<$self-E<gt>call_methylation(HASH)>

BSMAP methylation calling

  sam           sam files
  output        output
  delete_input  whether delete input files
 
=cut
sub call_methylation {
	my $self = shift;
	
	my %param = ( "sam" => undef,
	               "output" => undef,
	               "delete_input" => 0,
				   @_);
	
	my $sam    = to_abs_path($param{sam});
	my $output = to_abs_path($param{output});
	my $delete_input = $param{delete_input};
	
	my $pipeline = $self->{pipeline};
	
	$pipeline->add_command("python $BSMAP_BIN_DIR/methratio.py -d $BSMAP_GENOME_DIR/$BSMAP_REF_GENOME -o $output -p $sam");

	my $qid = $pipeline->run("-N" => $pipeline->get_job_name ? $pipeline->get_job_name : "_bsmap_call_methylation",
							 "-l" => { nodes => "1:ppn=1:lsdf", 
									    mem => "40GB",
										walltime => "100:00:00"});

	return($qid);
}

1;
